global proc ApplyChanFrame(string $ObjectName,float $FrameIndex,float $x,float $y,float $z,float $Pitch,float $Yaw,float $Roll)
{
    currentTime $FrameIndex;
    
    setAttr ($ObjectName+".tx") $x;
    setKeyframe ($ObjectName+".tx");
    setAttr ($ObjectName+".ty") $y;
    setKeyframe ($ObjectName+".ty");
    setAttr ($ObjectName+".tz") $z;
    setKeyframe ($ObjectName+".tz");
  
    setAttr ($ObjectName+".rotateX") $Pitch;
    setKeyframe ($ObjectName+".rx");
    setAttr ($ObjectName+".ry") $Yaw;
    setKeyframe ($ObjectName+".ry");
    setAttr ($ObjectName+".rz") $Roll;
    setKeyframe ($ObjectName+".rz");

}

global proc LoadAndApplyChanFile()
{
    string $ChanFilter = "*.chan";
    $FileNames = `fileDialog2 -fileMode 1 -fileFilter $ChanFilter`;
    string $FileName = $FileNames[0];
    print $FileName;
    // $FileName  = "C:/Users/sreeves/Downloads/CameraPath_55245056_55265956_left_to_front_to_right.chan";
    
    $ObjectNames = `ls -selection`;
    // print $ObjectNames;
    string $ObjectName = $ObjectNames[0];
    // print $ObjectName;

    // open the file for reading
    $fileId = `fopen $FileName "r"`;

    // get the first line of text
    string $nextLine = `fgetline $fileId`;

    while ( size( $nextLine ) > 0 )
    {
        //tokenize(split) line into separate elements of an array
        string $rawAnimArray[];
        tokenize ($nextLine, " ",$rawAnimArray);
        
        //place each element of the array into separate variables
        // print $rawAnimArray;
        float $FrameIndex=$rawAnimArray[0];
        float $x=$rawAnimArray[1];
        float $y=$rawAnimArray[2];
        float $z=$rawAnimArray[3];
        float $Pitch=$rawAnimArray[4];
        float $Yaw=$rawAnimArray[5];
        float $Roll=$rawAnimArray[6];

        //  change the currentTime and set keys for tx, ty, tz
        ApplyChanFrame( $ObjectName, $FrameIndex, $x, $y, $z, $Pitch, $Yaw, $Roll );

        //get the next line in the ascii file.
        $nextLine = `fgetline $fileId`;
    }
    //close file
    fclose $fileId;
}


LoadAndApplyChanFile();
